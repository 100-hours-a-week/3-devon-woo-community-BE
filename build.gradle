plugins {
	id 'java'
	id 'java-test-fixtures'
	id 'org.springframework.boot' version '3.4.10'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.devon'
version = '0.0.1-SNAPSHOT'
description = 'Tech Blog Web Application'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	// Spring Boot 스타터
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// MySQL
	runtimeOnly 'com.mysql:mysql-connector-j'

	// Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'

    // AoP
    implementation 'org.springframework.boot:spring-boot-starter-aop'

	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Spring Boot Configuration Processor
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // 이지미 스토리지
    implementation 'com.cloudinary:cloudinary-http44:1.32.0'

	// 테스트
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'com.h2database:h2'

	// 테스트 픽스처
	testFixturesImplementation 'org.springframework:spring-test'
}


// QueryDSL Q-class 생성 경로
def querydslDir = "$buildDir/generated/querydsl"

// QueryDSL 설정
sourceSets {
	main.java.srcDirs += [querydslDir]
}

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = file(querydslDir)
}

clean {
	delete file(querydslDir)
}

tasks.named('test') {
	useJUnitPlatform()

	// 사용 가능한 CPU 코어를 기준으로 병렬 실행 설정
	def cores = Runtime.runtime.availableProcessors()
	maxParallelForks = cores > 1 ? cores.intdiv(2) : 1

	finalizedBy jacocoTestReport
}

tasks.register('unitTest', Test) {
    useJUnitPlatform {
        includeTags 'unit'
    }
    def cores = Runtime.runtime.availableProcessors()
    maxParallelForks = cores > 1 ? cores.intdiv(2) : 1
    outputs.upToDateWhen { false }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform {
        includeTags 'integration'
    }
    def cores = Runtime.runtime.availableProcessors()
    maxParallelForks = cores > 1 ? cores.intdiv(2) : 1
    outputs.upToDateWhen { false }
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/Q*.class',
				'**/*Application.class',
				'**/*Config*.class',
				'**/dto/**',
				'**/exception/**'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/Q*.class',
				'**/*Application.class',
				'**/*Config*.class',
				'**/dto/**',
				'**/exception/**'
			])
		}))
	}
	violationRules {
		rule {
			limit {
				minimum = 0.50
			}
		}
	}
}
